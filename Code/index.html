<!DOCTYPE html>
<html lang="en">

<head>
    <meta charset="utf-8">
    <title>WegGL Example</title>

    <script type="text/javascript" src="gl-matrix-min.js"></script>
    <script type="text/javascript" src="main.js"></script>
    <script type="text/javascript" src="Shape.js"></script>
    <script type="text/javascript" src="helpers.js"></script>
    <script type="text/javascript" src="Transformations.js"></script>
    <script type="text/javascript" src="CreateShape.js"></script>
    <script type="text/javascript" src="UserInteraction.js"></script>
    <script type="text/javascript" src="ObjectParser.js"></script>
    <script type="text/javascript" src="ShaderProgram.js"></script>
    <script type="text/javascript" src="globals.js"></script>
    <link rel="stylesheet" href="canvas.css">

    <script id="v-shader-default" type="x-shader/x-vertex">
        precision mediump float;

        attribute vec4 vertexPosition; 
        attribute vec4 vertexColor;    

        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;

        varying vec4 fragmentColor;    

        void main() {
            gl_Position = projectionMatrix * modelViewMatrix * vertexPosition; 
            fragmentColor = vertexColor; 
        }
    </script>

    <script id="v-shader-gouraud-diffuse" type="x-shader/x-vertex">
        precision mediump float;

        attribute vec4 vertexPosition; 
        attribute vec4 vertexColor; 
        attribute vec3 vertexNormal;   

        uniform mat4 modelViewMatrix;
        uniform mat4 viewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;
        uniform vec4 lightLocation;

        varying vec4 fragmentColor;    

        void main() {

            vec4 lightPosition = viewMatrix * lightLocation;
            vec4 viewPosition =  modelViewMatrix * vertexPosition;

            vec3 lightVector = normalize(lightPosition.xyz - viewPosition.xyz);
            vec3 transformedNormal = normalize(normalMatrix * vertexNormal);

            vec4 ambientLight = vertexColor * vec4(0.3, 0.3, 0.3, 1);
            float lightIntensity = max(0.0, dot(lightVector, transformedNormal));

            gl_Position = projectionMatrix * viewPosition;
            fragmentColor = ambientLight + (lightIntensity * vertexColor);
            fragmentColor.a = 1.0; 
        }
    </script>

    <script id="v-shader-gouraud-specular" type="x-shader/x-vertex">
        precision mediump float;

        attribute vec4 vertexPosition; 
        attribute vec4 vertexColor; 
        attribute vec3 vertexNormal;   

        uniform mat4 modelViewMatrix;
        uniform mat4 viewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;
        uniform vec4 lightLocation;

        varying vec4 fragmentColor;    

        void main() {

            vec4 lightPosition = viewMatrix * lightLocation;
            vec4 viewPosition =  modelViewMatrix * vertexPosition;

            vec3 lightVector = normalize(lightPosition.xyz - viewPosition.xyz);
            vec3 transformedNormal = normalize(normalMatrix * vertexNormal);

            vec3 reflectVector = float(2) * dot(transformedNormal, lightVector) * transformedNormal - lightVector;

            vec3 eyeVector = vec3(-viewPosition);

            vec4 ambientLight = vertexColor * vec4(0.3, 0.3, 0.3, 1);
            float lightIntensity = max(0.0, dot(lightVector, transformedNormal));
            float specularLight = pow(max(0.0, dot(normalize(eyeVector), normalize(reflectVector))), 10.0);

            gl_Position = projectionMatrix * viewPosition;
            fragmentColor = ambientLight + (lightIntensity * vertexColor) + specularLight;
            fragmentColor.a = 1.0; 
        }
    </script>

    <script id="v-shader-phong" type="x-shader/x-vertex">
        precision mediump float;

        attribute vec4 vertexPosition;
        attribute vec4 vertexColor; 
        attribute vec3 vertexNormal;    

        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;

        varying vec3 transformedNormal;
        varying vec4 viewPosition;
        varying vec4 vertColor;

        void main() {

            vertColor = vertexColor;
            
            viewPosition =  modelViewMatrix * vertexPosition;

            transformedNormal = normalize(normalMatrix * vertexNormal);

            gl_Position = projectionMatrix * viewPosition; 
        }
    </script>

    <script id="f-shader-default" type="x-shader/x-fragment">
        precision mediump float;

        varying vec4 fragmentColor;

        void main() {
            gl_FragColor = fragmentColor;
        }
    </script>

    <script id="f-shader-phong-diffuse" type="x-shader/x-fragment">
        precision mediump float;

        uniform mat4 viewMatrix;
        uniform vec4 lightLocation;

        varying vec3 transformedNormal;
        varying vec4 viewPosition; 
        varying vec4 vertColor;
        
        void main() {
            vec4 lightPosition = viewMatrix * lightLocation;

            vec3 lightVector = normalize(lightPosition.xyz - viewPosition.xyz);

            vec4 ambientLight = vertColor * vec4(0.3, 0.3, 0.3, 1);
            float lightIntensity = max(0.0, dot(lightVector, transformedNormal));

            vec4 fragmentColor = ambientLight + (lightIntensity * vertColor);
            fragmentColor.a = 1.0; 

            gl_FragColor = fragmentColor;
        }
    </script>

    <script id="f-shader-phong-specular" type="x-shader/x-fragment">
        precision mediump float;

        uniform mat4 viewMatrix;
        uniform vec4 lightLocation;

        varying vec3 transformedNormal;
        varying vec4 viewPosition; 
        varying vec4 vertColor;
        
        void main() {
            vec4 lightPosition = viewMatrix * lightLocation;

            vec3 lightVector = normalize(lightPosition.xyz - viewPosition.xyz);

            vec3 reflectVector = float(2) * dot(transformedNormal, lightVector) * transformedNormal - lightVector;

            vec3 eyeVector = vec3(-viewPosition);

            vec4 ambientLight = vertColor * vec4(0.3, 0.3, 0.3, 1);
            float lightIntensity = max(0.0, dot(lightVector, transformedNormal));
            float specularLight = pow(max(0.0, dot(normalize(eyeVector), normalize(reflectVector))), 10.0);

            vec4 fragmentColor = ambientLight + (lightIntensity * vertColor) + specularLight;
            fragmentColor.a = 1.0; 

            gl_FragColor = fragmentColor;
        }
    </script>
</head>

<body>
    <canvas id="canvas" width="1900" height="900"></canvas>
</body>


</html>